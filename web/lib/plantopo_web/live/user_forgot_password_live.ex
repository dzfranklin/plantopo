defmodule PlanTopoWeb.UserForgotPasswordLive do
  use PlanTopoWeb, :live_view
  require Logger
  alias PlanTopo.Accounts

  def render(assigns) do
    ~H"""
    <div class="max-w-sm mx-auto">
      <.header class="text-center">
        Forgot your password?
        <:subtitle>We'll send a password reset link to your inbox</:subtitle>
      </.header>

      <.simple_form :let={f} id="reset_password_form" for={%{}} as={:user} phx-submit="send_email">
        <.input field={{f, :email}} type="email" placeholder="Email" required />
        <:actions>
          <.button phx-disable-with="Sending..." class="w-full">
            Send password reset instructions
          </.button>
        </:actions>
      </.simple_form>
      <p class="mt-4 text-center">
        <.link href={~p"/account/register"}>Register</.link>
        |
        <.link href={~p"/account/login"}>Log in</.link>
      </p>
    </div>
    """
  end

  def mount(_params, _session, socket) do
    {:ok, socket}
  end

  def handle_event("send_email", %{"user" => %{"email" => email}}, socket) do
    if user = Accounts.get_user_by_email(email) do
      case Accounts.deliver_user_reset_password_instructions(
             user,
             &url(~p"/account/reset_password/#{&1}")
           ) do
        {:ok, _} ->
          nil

        {:error, error} ->
          Logger.info("Failed to deliver user password reset instructions: #{inspect(error)}")
      end
    end

    info =
      "If your email is in our system, you will receive instructions to reset your password shortly."

    {:noreply,
     socket
     |> put_flash(:info, info)
     |> redirect(to: ~p"/")}
  end
end
